plugins {
    id 'java'
    id 'org.springframework.boot' version '3.1.0'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'com.github.node-gradle.node' version '5.0.0'
}

group = 'com.github.vendigo'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '20'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    runtimeOnly 'org.postgresql:postgresql'
    annotationProcessor 'org.projectlombok:lombok'

    testRuntimeOnly 'com.h2database:h2'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.named('test') {
    useJUnitPlatform()
}

def webappDir = "$projectDir/src/main/webapp"

node {
    nodeProjectDir.set(file(webappDir))
}

sourceSets {
    main {
        resources {
            srcDirs += "$webappDir/dist"
        }
    }
}

tasks.register('npmBuild', NpmTask) {
    dependsOn "npmInstall"
    args = ["run", "build"]
}

processResources.dependsOn npmBuild